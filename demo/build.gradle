buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.github.triplet.gradle:play-publisher:2.7.5"
  }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"

final def runningOnCi = System.getenv("CI") != null
android {
  compileSdkVersion 29
  buildToolsVersion "29.0.3"
  signingConfigs {
    release {
      storeFile file("release.keystore")
      storePassword "password"
      keyAlias "keyAlias"
      keyPassword "password"
    }
  }
  buildTypes {
    release {
      shrinkResources true
      minifyEnabled true
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt")
      signingConfig signingConfigs.release
    }
  }
  defaultConfig {
    applicationId "otpview.demo"
    minSdkVersion 17
    targetSdkVersion 29
    versionCode runningOnCi ? "git rev-list --count HEAD".execute().text.toInteger() : Integer.MAX_VALUE
    versionName runningOnCi ? "git describe --abbrev=0".execute().text.trim() : String.valueOf(versionCode)
    resConfigs "en"
  }
  variantFilter {
    if (it.buildType.name == "debug") {
      it.ignore = true
    }
  }
}

lint.onlyIf {
  false
}

repositories {
  google()
  mavenCentral()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
  implementation "androidx.constraintlayout:constraintlayout:1.1.3"
  implementation "androidx.appcompat:appcompat:1.1.0"
  // Replace by "com.github.stoyicker.otpview:library:$version" in your build.gradle
  implementation project(path: ":library")
}

apply plugin: "com.github.triplet.play"
play {
  enabled = runningOnCi
  track = "production"
  releaseStatus = "completed"
  userFraction = 1
  serviceAccountCredentials = file("${project.rootDir.absolutePath}/api-release.json")
  defaultToAppBundles = true
}
