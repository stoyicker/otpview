apply plugin: "com.android.library"

final def runningOnCi = System.getenv("CI") != null
android {
  buildToolsVersion "29.0.3"
  compileSdkVersion 29
  buildTypes {
    release {
      minifyEnabled true
      consumerProguardFiles "proguard/rules.pro"
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
  defaultConfig {
    // Depending on the git method locally causes IDE issues
    versionCode runningOnCi ? "git rev-list --count HEAD".execute().text.toInteger() : Integer.MAX_VALUE
    versionName runningOnCi ? System.getenv("PROJECT_VERSION") : String.valueOf(versionCode)
    minSdkVersion 17
    targetSdkVersion 29
  }
  dexOptions {
    preDexLibraries false
    javaMaxHeapSize "8g"
  }
  lintOptions {
    ignoreTestSources true
    abortOnError true
    absolutePaths true
    checkAllWarnings true
    disable "WrongThreadInterprocedural"
    checkDependencies true
    quiet false
    warningsAsErrors true
  }
  variantFilter {
    if (it.buildType.name.toLowerCase(Locale.ENGLISH) == "debug") {
      setIgnore(true)
    }
  }
}

repositories {
  google()
  jcenter()
}

dependencies {
  compileOnly "androidx.annotation:annotation:1.1.0"
  // From here we only "need" a constant to inline it, so no need for it to be transitive
  compileOnly("androidx.autofill:autofill:1.0.0") {
    transitive = false
  }
}

gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
  }
}

