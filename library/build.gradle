import java.nio.file.Paths

buildscript {
  repositories {
    mavenCentral()
  }
}

apply plugin: "com.android.library"
apply plugin: "maven-publish"
apply plugin: "signing"
apply from: Paths.get(rootDir.absolutePath, '.circleci', 'publish.gradle')

final def runningOnCi = System.getenv("CI") != null
android {
  namespace = "org.stoyicker.otpview"
  buildToolsVersion "34.0.0"
  compileSdkVersion 34
  buildTypes {
    release {
      minifyEnabled false
      consumerProguardFiles "proguard/rules.pro"
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  defaultConfig {
    // Depending on the git method locally causes IDE issues
    versionCode runningOnCi ? "git rev-list --count HEAD".execute().text.toInteger() : Integer.MAX_VALUE
    versionName runningOnCi ? "git describe --abbrev=0".execute().text.trim() : String.valueOf(versionCode)
    minSdkVersion 17
    targetSdkVersion 34
  }
  dexOptions {
    preDexLibraries false
    javaMaxHeapSize "8g"
  }
  lintOptions {
    ignoreTestSources true
    abortOnError true
    absolutePaths true
    checkAllWarnings true
    disable "WrongThreadInterprocedural"
    checkDependencies true
    quiet false
    warningsAsErrors true
  }
  variantFilter {
    if (it.buildType.name.toLowerCase(Locale.ENGLISH) == "debug") {
      setIgnore(true)
    }
  }
}

repositories {
  google()
  mavenCentral()
}

dependencies {
  compileOnly "androidx.annotation:annotation:1.6.0"
  // From here we only "need" a constant to inline it, so no need for it to be transitive
  compileOnly("androidx.autofill:autofill:1.1.0") {
    transitive = false
  }
}

gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
  }
}
